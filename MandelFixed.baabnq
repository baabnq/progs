"translated form: http://rosettacode.org/wiki/Mandelbrot_set#B

use 'libs/math.baabnq';
use 'libs/stack.baabnq';
use 'libs/twoc.baabnq';




put _cx = 0;
put _cy = 0;
put _x  = 0;
put _y  = 0;
put _x2 = 0;
put _y2 = 0;

put _iter = 0;

put _xmin    = 0 - 8601;
put _xmax    = 2867;
put _ymin    = 0 - 4915;
put _ymax    = 4915;

put _maxiter = 32;

put _dx = 145;
put _dy = 409;


put _cy = _ymin;
lab LoopY;
    jump DoneY ~ (_ymax - _cy) >> 15 != 0;
    put _cx = _xmin;
    
    lab LoopX;
        jump DoneX ~ (_xmax - _cx) >> 15 != 0;
        

        put _x = 0;
        put _y = 0;
        put _x2 = 0;
        put _y2 = 0;

        put _iter = 0;        
        lab Iter;
            jump IterDone ~ _iter > _maxiter;
                  

            push _x2 + _y2;
            push 16384;
            sub Twoc::Cmp;
            pull _temp;
            jump IterDone ~ _temp != 0;

            "y = ((x * y) << 1) + cy
            push _x;
            push _y;
            sub Twoc::Mul;
            push 1;
            sub Twoc::Shift;
            pull _temp;
            put _y = _temp + _cy;

            "x = (x2 - y2) + cx
            put _x = (_x2 - _y2) + _cx;

            "x = x >> 6
            push _x;
            push 0 - 6;
            sub Twoc::Shift;
            pull _x;

            "y = y >> 6
            push _y;
            push 0 - 6;
            sub Twoc::Shift;
            pull _y;

            "x2 = x * x
            push _x;
            push _x;
            sub Twoc::Mul;
            pull _x2;

            "y2 = y * y
            push _y;
            push _y;
            sub Twoc::Mul;
            pull _y2;

            put _iter = _iter + 1;
            jump Iter;
        lab IterDone;
        
        putchr _iter + 32;
        
        put _cx = _cx + _dx;        
        jump LoopX;
    lab DoneX;

    putchr 10;

    put _cy = _cy + _dy;
    jump LoopY;
lab DoneY;











