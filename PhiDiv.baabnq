use 'libs/math.baabnq';

"computes approximation of the golden ratio (phi),
"using two large fibonacci numbers.

put _a = 0;
put _b = 1;
put _c = 0;

lab FibLoop;
    put _c = _a + _b;
    put _a = _b;
    put _b = _c;
jump FibLoop ~ _c < 10946;


put _doRadixPoint = 1;

put _i = 0;
lab PrintLoop;
    push _i;

    "in-frame for Math::Mod
    push _b;
    push _a;

    "in-frame for Math::Div
    push _b;
    push _a;

    sub Math::Div;
    pull _d;
    putchr _d + 48;

    jump SkipRadixPoint ~ _doRadixPoint != 1;
        putchr 46;
        put _doRadixPoint = 0;
    lab SkipRadixPoint;

    sub Math::Mod;
    push 10;
    sub Math::Mul;
    pull _b;
    
    pull _i;
    put _i = _i + 1;
jump PrintLoop ~ _i < 10;

putchr 10;
